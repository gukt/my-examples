// 所有子项目的通用配置
subprojects { 
    apply plugin: 'java'
    // apply plugin: 'eclipse'
    apply plugin: 'idea'

    version '1.0-SNAPSHOT'

    // JVM 版本号要求
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    // java编译的时候缺省状态下会因为中文字符而失败
    // [compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'
    
    //定义版本号
    ext {  
        springVersion           = '4.1.6.RELEASE'  
        hibernateVersion        = '4.3.5.Final'  
    } 

    repositories {
        // 添加国内源，提高下载速度
//        maven { url 'http://maven.oschina.net/content/groups/public/' }
//        maven { url 'http://maven.oschina.net/content/groups/thirdparty/' }

        maven { url 'http://localhost:8081/nexus/content/groups/public/' }

        mavenLocal()
        jcenter()
        mavenCentral()
    }

    jar {
        manifest {
            attributes("Implementation-Title": "Gradle")
        }
    }

    configurations { 
        // 所有需要忽略的包定义在此
        all*.exclude group: 'commons-httpclient'
        // all*.exclude group: 'commons-logging'
        all*.exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }

    dependencies {
        // 通用依赖
        compile(
            "ch.qos.logback:logback-classic:1.1.2",
            "org.slf4j:slf4j-api:1.7.7",
        )

        // 依赖maven中不存在的jar
        ext.jarTree = fileTree(dir: 'libs', include: '**/*.jar')
        ext.rootProjectLibs = new File(rootProject.rootDir, 'libs').getAbsolutePath()
        ext.jarTree += fileTree(dir: rootProjectLibs, include: '**/*.jar')

        compile jarTree

        // 测试依赖
        testCompile(  
            "junit:junit:4.11"
        ) 
    }

    // 显示当前项目下所有用于 compile 的 jar.
    task listJars(description: 'Displays all compile jars.') << {
        configurations.compile.each { File file -> println '    ' + file.name }
    }

    task createDefaultDirs(description: 'Generates default java project structures') << {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
}

project(":spring") {
    dependencies {
        compile(
                "org.springframework:spring-context:$springVersion",

                'com.fasterxml.jackson.core:jackson-core:2.4.2',
                'com.fasterxml.jackson.core:jackson-annotations:2.4.2',
                'com.fasterxml.jackson.core:jackson-databind:2.4.2'
        )

        testCompile(
                "org.springframework:spring-test:$springVersion",
        )
    }
}

project(":misc") {
    dependencies {
        compile(
                "org.springframework:spring-context:$springVersion",
                "com.google.guava:guava:18.0",
                'com.google.protobuf:protobuf-java:2.6.1',
                "io.grpc:grpc-all:0.9.0",
                'commons-io:commons-io:2.4',
                'org.apache.mina:mina-core:2.0.7',
                'org.zeromq:jeromq:0.3.5',
                'org.quartz-scheduler:quartz:2.2.2'


        )
    }
}

project(":zeromq") {
    dependencies {
        compile(
                'org.zeromq:jeromq:0.3.5',
        )
    }
}

project(":mybatis") {
    dependencies {
        compile(
                'org.mybatis:mybatis:3.3.0'

        )
    }
}

project(":web") {
    dependencies {
        compile(
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-oxm:$springVersion",
                "org.springframework:spring-webmvc:$springVersion",
                'org.springframework:spring-support:2.0.8',

                "org.hibernate:hibernate-core:$hibernateVersion",
                'org.hibernate:hibernate-validator:5.2.2.Final',

                'javax.servlet:javax.servlet-api:3.1.0',
                'javax.persistence:persistence-api:1.0.2',
                'javax.validation:validation-api:1.1.0.Final',
                "com.google.guava:guava:18.0",
                "com.google.code:kaptcha:2.3.2",
                'javax.mail:mail:1.4.7',
                'org.apache.velocity:velocity:1.7',

                'commons-codec:commons-codec:1.10',

                'com.fasterxml.jackson.core:jackson-core:2.4.2',
                'com.fasterxml.jackson.core:jackson-annotations:2.4.2',
                'com.fasterxml.jackson.core:jackson-databind:2.4.2',

        )

        testCompile(
                "org.springframework:spring-test:$springVersion",
                'org.hamcrest:hamcrest-all:1.3'

        )
    }
}


